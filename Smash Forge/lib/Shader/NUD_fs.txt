#version 330

in vec3 viewPosition;
in vec3 objectPosition;
in vec2 texCoord;
in vec2 texCoord2;
in vec2 texCoord3;
in vec2 normaltexCoord;
in vec4 vertexColor;
in vec3 normal;
in vec3 viewNormal;
in vec3 tangent;
in vec3 bitangent;

out vec4 fragColor;

// Textures
uniform sampler2D dif;
uniform sampler2D dif2;
uniform sampler2D dif3;
uniform sampler2D ramp;
uniform sampler2D dummyRamp;
uniform sampler2D normalMap;
uniform sampler2D ao;
uniform samplerCube cube;
uniform samplerCube stagecube;
uniform sampler2D spheremap;
uniform samplerCube cmap;
uniform sampler2D UVTestPattern;

// flags tests
uniform int hasDif;
uniform int hasDif2;
uniform int hasDif3;
uniform int hasStage;
uniform int hasCube;
uniform int hasNrm;
uniform int hasRamp;
uniform int hasAo;
uniform int hasSphereMap;
uniform int hasDummyRamp;
uniform int hasColorGainOffset;
uniform int hasSpecularParams;
uniform int useDiffuseBlend;
uniform int hasDualNormal;
uniform int hasSoftLight;
uniform int hasCustomSoftLight;
uniform int hasFinalColorGain;
uniform int useDifRefMask;
uniform int hasBayoHair;
uniform int softLightBrighten;
uniform int hasUniverseParam;

// Da Flags
uniform uint flags;

// Check if src, dst, alpha function are non zero.
uniform int isTransparent;

uniform int lightSet;
uniform int isStage;
uniform int renderStageLighting;

// NU_ Material Properties
uniform vec4 colorOffset;
uniform vec4 aoMinGain;
uniform vec4 fresnelColor;
uniform vec4 specularColor;
uniform vec4 specularColorGain;
uniform vec4 diffuseColor;
uniform vec4 characterColor;
uniform vec4 colorGain;
uniform vec4 finalColorGain;
uniform vec4 finalColorGain2;
uniform vec4 finalColorGain3;
uniform vec4 reflectionColor;
uniform vec4 fogColor;
uniform vec4 effColorGain;
uniform vec4 zOffset;

// NU_ material params
uniform vec4 fresnelParams;
uniform vec4 specularParams;
uniform vec4 reflectionParams;
uniform vec4 fogParams;
uniform vec4 normalParams;
uniform vec4 angleFadeParams;
uniform vec4 dualNormalScrollParams;
uniform vec4 alphaBlendParams;
uniform vec4 softLightingParams;
uniform vec4 customSoftLightParams;
uniform vec4 effUniverseParam;

// Render Settings
uniform int renderDiffuse;
uniform int renderSpecular;
uniform int renderFresnel;
uniform int renderReflection;
uniform int renderType;
uniform int renderLighting;
uniform int renderVertColor;
uniform int renderNormal;
uniform int useNormalMap;
uniform int renderAlpha;

uniform float diffuseIntensity;
uniform float ambientIntensity;
uniform float specularIntensity;
uniform float fresnelIntensity;
uniform float reflectionIntensity;

// Character Lighting
uniform vec3 difLightColor;
uniform vec3 ambLightColor;
uniform vec3 difLightDirection;

uniform vec3 difLight2Color;
uniform vec3 ambLight2Color;
uniform vec3 difLight2Direction;

uniform vec3 difLight3Color;
uniform vec3 ambLight3Color;
uniform vec3 difLight3Direction;

uniform vec3 specLightColor;
uniform vec3 specLightDirection;

// Shared by characters and stages.
uniform vec3 fresGroundColor;
uniform vec3 fresSkyColor;
uniform vec3 fresSkyDirection;
uniform vec3 fresGroundDirection;

// Not found in game yet.
uniform vec3 refLightColor;

// Stage Light 1
uniform int renderStageLight1;
uniform vec3 stageLight1Color;
uniform vec3 stageLight1Direction;

// Stage Light 2
uniform int renderStageLight2;
uniform vec3 stageLight2Color;
uniform vec3 stageLight2Direction;

// Stage Light 3
uniform int renderStageLight3;
uniform vec3 stageLight3Color;
uniform vec3 stageLight3Direction;

// Stage Light 4
uniform int renderStageLight4;
uniform vec3 stageLight4Color;
uniform vec3 stageLight4Direction;

// light_set fog
uniform int renderFog;
uniform vec3 stageFogColor;

// Wireframe Rendering
uniform int colorOverride;

// Matrices
uniform mat4 mvpMatrix;
uniform mat4 modelViewMatrix;
uniform mat4 sphereMapMatrix;
uniform mat4 rotationMatrix;


uniform sampler2D shadowMap;

uniform vec3 NSC;
uniform float elapsedTime;

uniform vec3 cameraPosition;

// Constants
#define gamma 2.2
#define PI 3.14159

float Luminance(vec3 rgb)
{
    const vec3 W = vec3(0.2125, 0.7154, 0.0721);
    return dot(rgb, W);
}

vec3 TintColor(vec3 diffuseColor, float tintAmount)
{
    // Approximates an overlay blend mode. Cheaper than converting to HSV/HSL.
    // Normalize the color to avoid color tint making the model darker.
    vec3 colorTint = mix(vec3(1), (normalize(diffuseColor) * 2), min(tintAmount * 0.5, 1));
    return colorTint;
}

float ShadowCalculation(vec4 fragPosLightSpace)
{
    vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
    projCoords = projCoords * 0.5 + 0.5;
    float closestDepth = texture(shadowMap, projCoords.xy).r;
    float currentDepth = projCoords.z;
    float shadow = currentDepth > closestDepth  ? 1.0 : 0.0;

    return shadow;
}

vec3 BumpMapNormal(vec3 inputNormal)
{
    if(hasNrm == 0 || useNormalMap == 0)
	   return inputNormal;


    // TODO: How does NU_dualNormalScrollParams work?
    vec3 bumpMapNormal = texture(normalMap, normaltexCoord).xyz;
    vec3 bumpMapNormal2 = texture(normalMap, vec2(normaltexCoord.x + dualNormalScrollParams.x, normaltexCoord.y + dualNormalScrollParams.y)).xyz;
    if(hasDualNormal == 1)
        bumpMapNormal = normalize(bumpMapNormal + bumpMapNormal2);

    // Change normal map intensity.
    bumpMapNormal = mix(vec3(0.5, 0.5, 1), bumpMapNormal, normalParams.x);

    bumpMapNormal = 2.0 * bumpMapNormal - vec3(1);
    mat3 tbnMatrix = mat3(tangent, bitangent, normalize(normal));
    vec3 newNormal = tbnMatrix * bumpMapNormal;
    return normalize(newNormal);
}

vec3 ScreenBlend(vec3 base, vec3 top)
{
    return vec3(1) - (vec3(1) - base) * (vec3(1) - top);
}

vec3 RampColor(float rampCoord){
    // TODO: Vertical component is always 0?
	if(hasRamp == 1)
	{
		rampCoord = clamp(rampCoord, 0.01, 0.99);
		return texture(ramp, vec2(1 - rampCoord, 0.0)).rgb;
	}
	else
		return vec3(0);
}

vec3 DummyRampColor(float rampCoord){
    // TODO: Vertical component is always 0?
	if(hasDummyRamp == 1)
	{
		rampCoord = clamp(rampCoord, 0.01, 0.99);
		return texture(dummyRamp, vec2(1 - rampCoord, 0.0)).rgb;
	}
	else
		return vec3(0);
}

vec3 SphereMapColor(vec3 N)
{
    if (hasSphereMap == 1)
    {
        // Calculate UVs based on view space normals.
        vec2 sphereTexcoord = vec2(viewNormal.x, (1 - viewNormal.y));
        return texture(spheremap, sphereTexcoord).xyz;
    }
    else
        return vec3(0);
}

vec3 ShiftTangent(vec3 tangent, vec3 normal, float shift) // probably not needed
{
    vec3 shiftedT = tangent + shift * normal;
    return normalize(shiftedT);
}

float Fresnel(vec3 I, vec3 N)
{
    return max(1 - max(dot(I, N), 0), 0);
}

vec3 BayoHairSpecular(vec3 diffuseMap, vec3 I, float xComponent, float yComponent)
{
    // TODO: Figure out how this actually works...
    float shiftTex = diffuseMap.g; // vertical component of ramp?
    shiftTex = 0;

    vec3 t1 = ShiftTangent(tangent.xyz, normal.xyz, shiftTex);
    vec3 t2 = ShiftTangent(bitangent.xyz, normal.xyz, shiftTex);

    float specExp1 = reflectionParams.z;
    float specExp2 = reflectionParams.w;

    vec3 hairSpecular =  vec3(1);
    float specMask = diffuseMap.b; // what channel should this be?

    vec3 halfAngle = normalize(I + specLightDirection);
    float test = dot(t2, halfAngle)/reflectionParams.w;
    test = (test + 1) / 2;
    float test2 = diffuseMap.g;

    // TODO: Find proper constants.
    hairSpecular = texture(dummyRamp, vec2(test, test2)).rgb * diffuseMap.b * alphaBlendParams.z * 0.1;
    return (hairSpecular) * diffuseMap.r * 20;
}

vec3 SoftLighting(vec3 diffuse, vec4 params, float darkenMultiplier,
    float saturationMultiplier, float halfLambert)
{
    // Higher blend values make the dark region smoother and larger.
    float edgeL = 0.5;
    float edgeR = 0.5 + (params.z / 2);
    float smoothLambert = smoothstep(edgeL, edgeR, halfLambert);

    // Controls ambient brightness.
    float ambientGain = max(1 - (darkenMultiplier * params.y), 0);

    // Controls ambient saturation.
    // TODO: Need to check the math.
    // This next level math hacking may or may not be right.
    vec3 ambientTintColor = normalize(pow(diffuse, vec3(params.x * 0.3)));

    // Creates a custom diffuse gradient rather than using lighting?
    vec3 result = diffuse * mix(ambientTintColor * ambientGain, vec3(1), smoothLambert);
    return result;
}

vec3 FresnelPass(vec3 N, vec3 I, vec4 diffuseMap, float aoMap, float aoMixIntensity, vec3 tintColor)
{
    vec3 fresnelPass = vec3(0);

    // hemisphere fresnel with fresnelParams exponent
    float hemiBlendSky = dot(N, fresSkyDirection) * 0.5 + 0.5;
    vec3 hemiColorSky = mix(vec3(0), fresSkyColor, hemiBlendSky);

    float hemiBlendGround = dot(N, fresGroundDirection) * 0.5 + 0.5;
    vec3 hemiColorGround = mix(vec3(0), fresGroundColor, hemiBlendGround);

    // TODO: Find a more accurate value.
    float exponentOffset = 2.75;

    vec3 fresnel = clamp(((hemiColorSky + hemiColorGround) * pow(Fresnel(I, N), exponentOffset + fresnelParams.x)), 0, 1);
    fresnelPass += fresnelColor.rgb * fresnel * fresnelIntensity * tintColor;

    if ((flags & 0x0000FF00u) == 0x00003000u)
        fresnelPass *= diffuseMap.rgb;

    fresnelPass *= mix(aoMap, 1, aoMixIntensity);

    return fresnelPass;
}

vec3 ReflectionPass(vec3 N, vec3 I, vec4 diffuseMap, float aoMap, float aoMixIntensity, vec3 tintColor)
{
    vec3 reflectionPass = vec3(0);
	// cubemap reflection
	vec3 R = reflect(I, N);
	R.y *= -1.0;
	vec3 stageCubeColor = texture(stagecube, R).rgb;

    // TODO: Cubemaps from model.nut currently just use the miiverse cubemap.
	if (hasCube == 1)
		reflectionPass += diffuseMap.aaa * stageCubeColor * tintColor * reflectionParams.x;

    // TODO: Stage cubemaps currently just use the miiverse cubemap.
    if (hasStage == 1)
       reflectionPass += reflectionColor.rgb * stageCubeColor * tintColor;

	reflectionPass += SphereMapColor(normal.xyz) * reflectionColor.xyz * tintColor;



    reflectionPass *= mix(aoMap, 1, aoMixIntensity);
    reflectionPass *= refLightColor;
    reflectionPass *= reflectionIntensity;

    if (useDifRefMask == 1)
        reflectionPass *= diffuseMap.a;

    reflectionPass = pow(reflectionPass, vec3(gamma));
    return reflectionPass;
}

float AnisotropicSpecExponent(vec3 halfAngle, float width, float height)
{
    // Blinn-phong with some anistropic bits stolen from an anistropic GGX BRDF.
    vec3 X = normalize(tangent);
    vec3 Y = normalize(bitangent);
    float xComponent = max(pow((dot(halfAngle, X) / width), 2), 0);
    float yComponent = max(pow((dot(halfAngle, Y) / height), 2), 0);

    return xComponent + yComponent;
}

vec3 SpecularPass(vec3 N, vec3 I, vec4 diffuseMap, float aoMap, float aoMixIntensity, vec3 tintColor)
{
    vec3 specularPass = vec3(0);

    // Only uses the anisotropic exponent for mats without NU_specularParams.
    vec3 halfAngle = normalize(I + specLightDirection);
    float exponent = AnisotropicSpecExponent(halfAngle, reflectionParams.z, reflectionParams.w);
    if (hasSpecularParams == 1)
        exponent = specularParams.y;

    float blinnPhongSpec = dot(halfAngle, N);
    blinnPhongSpec = pow(blinnPhongSpec, exponent);

    vec3 specColorTotal = specularColor.rgb * blinnPhongSpec;

    // TODO: Fix bayo hair calculations.
    if (hasBayoHair == 1)
        specColorTotal = BayoHairSpecular(diffuseMap.rgb, I, reflectionParams.z, reflectionParams.w);

    // TODO: Saturation is incorrect for Shulk's hair.
    if (hasColorGainOffset == 1)
        specColorTotal *= specularColorGain.rgb;

    // TODO: Not sure how this works. Specular works differently for eye mats.
    if ((flags & 0x00E10000u) == 0x00E10000u)
        specColorTotal *= 0;

    specularPass += specColorTotal;

    specularPass *= mix(aoMap, 1, aoMixIntensity);
    specularPass *= tintColor;
    specularPass *= specLightColor;
    specularPass *= specularIntensity;

    specularPass = pow(specularPass, vec3(gamma));
    return specularPass;
}

float AngleFade(vec3 N, vec3 I)
{
    float fresnelBlend = Fresnel(I, N);
    float angleFadeAmount = mix(angleFadeParams.x, angleFadeParams.y, fresnelBlend);
    return max((1 - angleFadeAmount), 0);
}

vec3 FogColor(vec3 inputColor)
{
    float depth = viewPosition.z;
    depth = clamp((depth / fogParams.y), 0, 1);
    float fogIntensity = mix(fogParams.z, fogParams.w, depth);
    if(renderFog == 1 && renderStageLighting == 1)
        return mix(inputColor, pow((stageFogColor), vec3(gamma)), fogIntensity);
    else
        return inputColor;
}

vec3 UniverseColor(float universeParam)
{
    // TODO: Doesn't work for all angles (viewing from below).

    // Account for camera position and rotation for projection.
    vec3 projVec = normalize(objectPosition.xyz - cameraPosition.xyz);
    projVec = mat3(modelViewMatrix) * projVec;
    // Cheap approximations of trig functions.
    float uCoord = projVec.x * 0.5 + 0.5;
    float vCoord = projVec.y * 0.5 + 0.5;
    // Gamma correct again to make the texture dark enough.
    return pow(texture(dif, vec2(uCoord * universeParam, 1 - vCoord)).rgb, vec3(gamma));
}

vec3 Lighting(vec3 N, float halfLambert)
{
    // Flags for ignoring stage lighting. TODO: Replace bitwise operations.
    if (((flags & 0x0F000000u) < 0x04000000u) || renderStageLighting != 1)
        return vec3(1);

    vec3 lighting = vec3(1);

    if (isStage == 1) // stage lighting
    {
        // TODO: Do stages use half lambert?
        vec3 stageLight1 = stageLight1Color * max((dot(N, stageLight1Direction)), 0);
        vec3 stageLight2 = stageLight2Color * max((dot(N, stageLight2Direction)), 0);
        vec3 stageLight3 = stageLight3Color * max((dot(N, stageLight3Direction)), 0);
        vec3 stageLight4 = stageLight4Color * max((dot(N, stageLight4Direction)), 0);

        lighting = vec3(0);
        lighting += (stageLight1 * renderStageLight1);
        lighting += (stageLight2 * renderStageLight2);
        lighting += (stageLight3 * renderStageLight3);
        lighting += (stageLight4 * renderStageLight4);
    }
    else
    {
        // Character lighting.
        lighting = vec3(0);
        lighting += mix(ambLightColor * ambientIntensity, difLightColor * diffuseIntensity, halfLambert);
    }

    return lighting;
}

vec3 AOBlend(float aoMap)
{
    // Calculate the effect of NU_aoMinGain on the ambient occlusion map.
    // This is only used for diffuse shading.
    float maxAOBlendValue = 1.25;
    vec3 aoBlend = vec3(aoMap);
    // TODO: Fix aoBlend saturation.
    return min((aoBlend + aoMinGain.rgb), vec3(maxAOBlendValue));
}

vec3 DiffusePass(vec3 N, vec4 diffuseMap, float aoMap)
{
    vec3 diffusePass = vec3(0);

    if (hasUniverseParam == 1)
        return UniverseColor(effUniverseParam.x);

    // lambertian shading
    float halfLambert = dot(difLightDirection, N) * 0.5 + 0.5;
    float halfLambert2 = dot(difLight2Direction, N) * 0.5 + 0.5;
    float halfLambert3 = dot(difLight3Direction, N) * 0.5 + 0.5;
    vec3 diffuseColorFinal = vec3(0); // result of diffuse map, aoBlend, and some NU_values
    float diffuseLuminance = Luminance(diffuseMap.rgb); // used for colorGain and colorOffset

    // Ambient occlusion calculations
    vec3 aoBlend = AOBlend(aoMap);
    float aoMixIntensity = aoMinGain.a;

    if (hasColorGainOffset == 1) // probably a more elegant solution...
    {
        diffuseColorFinal = colorOffset.rgb + (Luminance(diffuseMap.rgb) * (colorGain.rgb));

        if (hasBayoHair == 1)
        {
            // Bayo hair diffuse has three different textures for R, G, and B.
            diffuseColorFinal = colorOffset.rgb + (diffuseMap.rrr * colorGain.rgb);
            diffuseColorFinal *= alphaBlendParams.w; // #justbayothings
            aoMap = 1; // TODO: Bayo hair AO blending.
        }
    }
    else
    {
        // Regular characters.
        diffuseColorFinal = diffuseMap.rgb * aoBlend * diffuseColor.rgb;
    }

    // Stage lighting
    vec3 lighting = Lighting(N, halfLambert);

    // Dummy ramp calculations.
    float rampIntensity = 0.75;
    vec3 rampTotal = (0.2 * RampColor(halfLambert)) + (0.75 * DummyRampColor(halfLambert));
    vec3 rampAdd = mix(vec3(0), rampTotal, halfLambert);

    diffusePass = diffuseColorFinal * lighting;
    // TODO: Fix ramp shading. Disabled for now.
    // diffusePass += diffusePass * min(rampAdd, vec3(1));

    vec3 softLightDif = diffuseColorFinal * difLightColor;
    vec3 softLightAmb = diffuseColorFinal * ambLightColor;
    if (hasSoftLight == 1)
        diffusePass = SoftLighting(softLightDif, softLightingParams, 0.3, 0.0561, halfLambert);
    else if (hasCustomSoftLight == 1)
        diffusePass = SoftLighting(softLightDif, customSoftLightParams, 0.3, 0.114, halfLambert);

    // Flags used for brightening diffuse for softlightingparams.
    if (softLightBrighten == 1)
        diffusePass *= 1.5;

    diffusePass = pow(diffusePass, vec3(gamma));
    return diffusePass;
}

vec2 DistortedUvCoords(vec2 texCoord)
{
    // Similar effect to du dv but uses just the normal map.
    // TODO: does du dv affect other UV channels?
    float offsetIntensity = 0;
    if(useNormalMap == 1)
        offsetIntensity = normalParams.z;
    vec2 textureOffset = 1 - texture(normalMap, normaltexCoord).xy;
    textureOffset = (textureOffset * 2) - 1; // Remap [0,1] to [-1,1]
    return texCoord + (textureOffset * offsetIntensity);
}

vec4 TotalDiffuseMap()
{
    // Some stage water materials have NU_diffuseColor but no diffuse map.
    if (hasDif != 1)
        return diffuseColor;

    vec2 offsetTexCoord = DistortedUvCoords(texCoord);

    // Blends all of the different diffuse textures together.
    vec4 diffuseMapTotal = vec4(0);
    if (hasDif == 1)
    {
        vec4 diffuse1 = texture(dif, offsetTexCoord) * finalColorGain.rgba;
        diffuseMapTotal = diffuse1;

        // TODO: 2nd diffuse texture. doesnt work properly with stages.
        if (hasDif2 == 1 && hasDif3 != 1)
        {
            vec4 diffuse2 = texture(dif2, texCoord2) * finalColorGain2.rgba;
            diffuseMapTotal = mix(diffuse2, diffuse1, diffuse1.a);
            diffuseMapTotal.a = 1.0;

            if (hasDif3 == 1)
            {
                vec4 diffuse3 = texture(dif3, texCoord3) * finalColorGain3.rgba;
                diffuseMapTotal = mix(diffuse3, diffuseMapTotal, diffuse3.a);
            }
        }
    }

    return diffuseMapTotal;
}

vec3 SmashShader(vec4 diffuseMap, float aoMap, vec3 N, vec3 I)
{
    float aoMixIntensity = aoMinGain.w;

	// Calculate the color tints
	vec3 specTintColor = TintColor(diffuseMap.rgb, specularColor.a);
	vec3 fresTintColor = TintColor(diffuseMap.rgb, fresnelColor.a);
	vec3 reflTintColor = TintColor(diffuseMap.rgb, reflectionColor.a);

    if (useDiffuseBlend == 1) // aomingain but no ao map (mainly for trophies)
    {
        // TODO: Should this use color? The party ball item is not saturated enough.
        aoMap = Luminance(diffuseMap.rgb);
        aoMixIntensity = 0;
    }

    // The diffuse map for colorGain/Offset materials does a lot of things.
    if (hasColorGainOffset == 1)
        aoMap = Luminance(diffuseMap.rgb);

    // Separate render pass calculations.
    vec3 diffusePass = DiffusePass(N, diffuseMap, aoMap);
    vec3 specularPass = SpecularPass(N, I, diffuseMap, aoMap, aoMixIntensity, specTintColor);
    vec3 fresnelPass = FresnelPass(N, I, diffuseMap, aoMap, aoMixIntensity, fresTintColor);
	vec3 reflectionPass = ReflectionPass(N, I, diffuseMap, aoMap, aoMixIntensity, reflTintColor);

	vec3 resultingColor = vec3(0);

	if(renderLighting == 1)
	{
        // Prevent negative colors for some GPUs.
    	resultingColor += max((diffusePass * renderDiffuse), 0);
    	resultingColor += max((fresnelPass * renderFresnel), 0);
    	resultingColor += max((specularPass * renderSpecular), 0);
    	resultingColor += max((reflectionPass * renderReflection), 0);

        // light_set_param.bin fog
        resultingColor = FogColor(resultingColor);
	}
	else
		resultingColor = diffusePass;

    resultingColor = pow(resultingColor, vec3(1 / gamma));
    return resultingColor;
}

void main()
{
    fragColor = vec4(0,0,0,1);

    if (colorOverride == 1)
    {
        // Wireframe color.
        fragColor = vec4(1);
        return;
    }

    // Global vectors.
    vec3 I = vec3(0,0,-1) * mat3(mvpMatrix);
    vec3 N = BumpMapNormal(normal);

    // zOffset correction
    // TODO: Divide by far plane?
    gl_FragDepth = gl_FragCoord.z - (zOffset.x / 1500);

    // Calculate diffuse map blending.
    vec4 diffuseMapTotal = TotalDiffuseMap();

    // TODO: Research how mii colors work.
    diffuseMapTotal *= characterColor.rgba;

    // TODO: Create uniform for flags comparison.
    if (hasAo == 1)
    {
        vec3 difAO = texture(ao, texCoord2).rgb;
        float normalBlend = normal.y * 0.5 + 0.5;

        if ((flags & 0x00410000u) == 0x00410000u)
            diffuseMapTotal.rgb = mix(difAO, diffuseMapTotal.rgb, normalBlend);
        else
            diffuseMapTotal.rgb *= difAO.rgb;
    }

    // Material lighting done in SmashShader
    fragColor.rgb = SmashShader(diffuseMapTotal, texture(normalMap, texCoord).a, N, I);

    // TODO: Max vertex color value?
    if (renderVertColor == 1 || hasFinalColorGain == 1)
        fragColor *= min(vertexColor, vec4(1));

    // Universe mats are weird...
    if (hasUniverseParam != 1)
        fragColor.rgb *= effColorGain.rgb;

    // Adjust the alpha.
    // TODO: Is this affected by multiple diffuse maps?
    if (renderAlpha == 1)
        fragColor.a = texture(dif, texCoord).a;

    fragColor.a *= finalColorGain.a;
    fragColor.a *= effColorGain.a;
    fragColor.a *= AngleFade(N, I);
    fragColor.a += alphaBlendParams.x; // TODO: Ryu works differently.

    // Alpha override for render settings. Fixes alpha for renders.
    if (renderAlpha != 1 || isTransparent != 1)
        fragColor.a = 1;
}
